! Morphological Transducer for Ainu

Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<vblex%> ! Verb
%<adv%> ! Adverb
%<adj%> ! Adjective
%<pn%> ! Pronoun
%<det%> ! Determiner
%<cnjsub%> ! subordinating conjunction

! Noun tags
%<abs%> ! Abstract qualities
%<m%> ! Noun male gender
%<f%> ! Noun female gender
%<mg%> ! Noun male god gender
%<mf%> ! Noun male or female gender
%<endear%> ! Noun affinity tag ("=my dear x")
%<honor%> ! Noun honorable tag ("=my honored x")
%<px1sg%> ! First person singular possessive
%<px2sg%> ! Second person singular possessive
%<px1pl%> ! First person plural possessive
%<px2pl%> ! Second person plural possessive
%<nx%> ! Nominalization
%<rn%> ! relational noun
%<bn%> ! bound noun
 
! Determiner tags
%<ndet%> ! numeral determiner
%<ndom%> ! nominal demonstrative
%<adom%> ! anaphoric demonstrative


! Verb tags
%<pasv1%> ! Passive Voice chi
%<pasv2%> ! Passive Voice a
%<tv%> ! Transitive Verb
%<iv%> ! Intransitive Verb
%<vaux%> ! Auxiliary Verb
%<vx%> ! Verbalization
%<tvx%> ! Make verb transitive
%<cop%> ! copula

! Tense tags
%<pres%> ! Present Tense

! Mood tags
%<pri%> ! Indicative Mood

! Person tags
%<p1%> ! First Person
%<p2%> ! Second Person


! Adjective tags
%<comp%> ! Comparative
%<sup%> ! Superlative
%<adjx%> ! Adjectivization

! Article tags
%<def%> ! Definite Article

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Other symbols
%>      ! Morpheme boundary
%<dim%> ! Diminutive Particle
%<int%> ! Intensifying Particle  
% 	! Space


! Archiphonemes
%{I%} ! can be イ or ヒ or null

%{i%} ! can be i or null

%{Mm%}  ! mu mm or null
%{Tt%}  ! tu tt or null
%{Kk%} ! ku kk or null
%{t%}  ! small tsu or null


%{pe%} ! pe or null
%{K%} ! ka, ki, ku, ke, or ko

%{U%}  ! all sorts of stuff

! Features
%[%+adjx%] ! + adjectivalization
%[%-adjx%] ! + adjectivalization

%[%+comp%] ! comparison
%[%-comp%]

%[%+sup%] ! 
%[%-sup%] ! 

%[%+dInd%] ! Indefinite determiner
%[%-dInd%]

%[%+dDef%] ! Definite determiner
%[%-dDef%]

%[%+tvx%] ! Making verb transitive
%[%-tvx%]

%[%+pl%] ! plurality
%[%-pl%] 

%[%+m%] ! male gender
%[%-m%]

%[%+f%] ! female gender
%[%-f%]

%[%+pasv1%] ! verb passification
%[%-pasv1%]

%[%+pasv2%] ! verb passification
%[%-pasv2%]

%[%+sg%] ! singular
%[%-sg%]

%[%+int%] ! intensifier
%[%-int%]

%[%+mg%] ! male (god)
%[%-mg%]

%[%+appl%] ! applicative
%[%-appl%]

%[%+apass%] ! generalized object
%[%-apass%]

%[%+refl%] ! reflexive
%[%-refl%]

%[%+rec%] ! reciprocal
%[%-rec%]

%[%+acaus%] ! acausative
%[%-acaus%]

%[%+pos%] ! possessive common noun
%[%-pos%]

%[%+pp1sg%] ! possessive relational noun
%[%-pp1sg%]

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Conjunction ;
Prefix ;
Determiner ;

LEXICON Prefix

%[%-pasv1%]:チ VerbRoot ;
%[%-tvx%]:エ VerbRoot ;
%[%-sup%]:イヨッタ%  VerbRoot ;
%[%-comp%]:ナア%  VerbRoot ;
%[%-pasv2%]:ア VerbRoot ;
%[%-int%]:イ VerbRoot ;
%[%-1ps%]:%{K%} VerbPrefix-ClassI ;
NounPrefix ;
AdjectiveRoot ;
VerbRoot ;
VerbPrefix-ClassI ;

LEXICON NounPrefix

%[%-dInd%]:シネ%  NounRoot ;
%[%-dDef%]:ネイ%  NounRoot ;
%[%-pl%]:ウ%> NounRoot ;
%[%-m%]:ピンネ%  NounRoot ;
%[%-f%]:マㇳネ%  NounRoot ;
%[%-mg%]:オッカイ%  NounRoot ;
%[%-sg%]:シネ%  NounRoot ;
%[%-pos%]:ク NounRoot ;
%[%-pp1sg%]:エン NounRoot ;
NounRoot ;

LEXICON VerbPrefix-ClassI 

! applicative
%[%-appl%]:エ VerbPrefix-ClassII ;
%[%-appl%]:コ VerbPrefix-ClassII ;
%[%-appl%]:オ VerbPrefix-ClassII ;


VerbPrefix-ClassII ;

LEXICON VerbPrefix-ClassII 

%[%-apass%]:イ VerbPrefix-ClassIII ;
%[%-refl%]:ヤィ VerbPrefix-ClassIII ;
%[%-rec%]:ウ VerbPrefix-ClassIII ;
%[%-acaus%]:シ VerbPrefix-ClassIII ;


VerbPrefix-ClassIII ;

LEXICON VerbPrefix-ClassIII 

%[%-appl%]:エ VerbPrefix-ClassII ;
%[%-appl%]:コ VerbPrefix-ClassII ;
%[%-appl%]:オ VerbPrefix-ClassII ;

VerbRoot ;

LEXICON RegVerbInfl

%<vblex%>:%> Diminutive ;
%<vblex%>%<tv%>%<pasv1%>%[%+pasv1%]:  # ;
%<vblex%>%<tv%>%<pasv2%>%[%+pasv2%]:  # ;
%<vblex%>%<int%>%[%+int%]:  # ;
%<vblex%>%<nx%>%<abs%>:%>% イ # ;
%<vblex%>%<nx%>%<abs%>:%>% ヒ # ;
%<vblex%>%<pasv1%>%[%+pasv1%]: NounInfl-Common ;
%<vblex%>%<pasv1%>%[%+pasv1%]: # ;
%<vblex%>%<pasv2%>%[%+pasv2%]: NounInfl-Common ;
%<vblex%>%<pasv2%>%[%+pasv2%]: # ;
%<vblex%>%<iv%>%<tvx%>%[%+tvx%]:  # ;
%<vblex%>%<vaux%>%<vpres%>:シリネ # ;
%<vblex%>%<pl%>:パ # ;
%<vblex%>%<comp%>%[%+comp%]:  # ;
%<vblex%>%<sup%>%[%+sup%]:  # ;
%<vblex%>:  VerbSuffix-ClassVI ;

LEXICON VerbSuffix-ClassVI 

%<caus%>:%>レ # ;

LEXICON Diminutive

%<dim%>:%>ポ # ;

LEXICON NounInfl-utara
%<n%>%<pl%>:%>% ウタラ # ;
NounInfl-Common ;

LEXICON NounInfl-U
%<n%>%<pl%>:%>%{U%} # ;
NounInfl-Common ;

LEXICON NounInfl-preU

%<n%>%<pl%>%[%+pl%]: # ;
NounInfl-Common ;

LEXICON NounInfl-Nu
%<n%>%<pl%>:%>ヌ # ;
NounInfl-Common ;

LEXICON NounInfl-Irreg
%<n%>%<pl%>:  # ;
NounInfl-Common ;

LEXICON NounInfl-Common

%<n%>: Diminutive ;
%<nx%>:%>% ㇷ゚ # ;
%<pn%>%<px1sg%>%<mf%>:%>コロ Diminutive ;
%<n%>%<vz%>:%>コロ  # ;
%<n%>%<vz%>:%>カラ  # ;
%<n%>%<det%>%<ind%>%[%+dInd%]:  # ;
%<n%>%<det%>%<def%>%[%+dDef%]:  # ;
%<n%>%<sg%>%[%+sg%]:  # ;
%<n%>%<m%>%[%+m%]:  # ;
%<n%>%<mg%>%[%+mg%]:  # ;
%<n%>%<f%>%[%+f%]:  # ;
%<n%>%<end1%>:%>% トノケ # ;
%<n%>%<end2%>:%>% ポ # ;
%<n%>%<hon%>:%>% ニㇱパケ # ;
%<n%>:  # ;
%<n%>%<cn%>%<pos%>%[%+pos%]:イヒ # ;


LEXICON NounInfl-Relational

%<n%>%<rn%>%<pos%>%<p1%>%<sg%>%[%+pp1sg%]:  # ;
%<n%>%<rn%>%<pos%>%<p3%>%<sg%>:ア # ;

LEXICON RegAdjectiveInfl

%<adj%>%<adv%>:%>タラ # ;
%<adj%>%<pl%>:%>パ # ;
%<adj%>:%> AbstractNoun ;
%<adj%>%<comp%>%[%+comp%]:  # ;
%<adj%>%<sup%>%[%+sup%]:  # ;
%<vblex%>%<iv%>%<nx%>%<abs%>:%>% イ # ;
%<vblex%>%<iv%>%<nx%>%<abs%>:%>% ヒ # ;
%<vblex%>%<iv%>%<nx%>:%>% ㇷ゚ # ; 
%<adj%>:  # ;

LEXICON AbstractNoun
%<n%>%<abs%>:ポ Diminutive ;

LEXICON IrregAdjectiveInfl

%<adv%>:%>ノ # ;


LEXICON Copula

%<vblex%>%<cop%>:  # ;
! RegVerbInfl ;

LEXICON CompoundAdj

%<adj%>:%>テ RegAdjectiveInfl ;

LEXICON ConjunctionInfl

%<cnjsub%>:  # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

house:house NounInfl-Common ; ! A noun
エ:エ NounInfl-Common ; ! a noun
チ:チ NounInfl-Common ; !
エチ:エチ NounInfl-Common ; !
ク:ク NounInfl-Common ; !
イセ:イセ NounInfl-Common ; !
ポンペ:ポンペ NounInfl-Common ; !
チカㇷ゚:チカㇷ゚ NounInfl-Common ; !
オッカィ:オッカィ NounInfl-Common ; !
チイカナィ:チイカナィ NounInfl-Common ; !
チㇱナィ:チㇱナィ NounInfl-Common ; !
ト゚カラ:ト゚カラ NounInfl-Common ; !
イキリ:イキリ NounInfl-Common ; ! a seam 
アット゚ㇱ:アット゚ㇱ NounInfl-Common ; ! cloth 
シセイ:シセイ NounInfl-Common ; ! a house
ハウ:ハウ NounInfl-Common ; ! the voice
ホニ:ホニ NounInfl-Common ; ! the stomach
カヤ:カヤ NounInfl-Common ; ! a sail
アイアイ:アイアイ NounInfl-utara ; !
アイヌ:アイヌ NounInfl-utara ; !
アシキペㇳ:アシキペㇳ NounInfl-U ; !
アㇳ:ア%{Tt%} NounInfl-U ; !
アㇺ:ア%{Mm%} NounInfl-U ; !
イクㇱペ:%{i%}クㇱペ NounInfl-preU ; !
イタㇰ:イタ%{Kk%} NounInfl-U ; !
イリワㇰ:イリワㇰ NounInfl-preU ; !
ウㇺマ:ウㇺマ NounInfl-utara ; !
クㇳ:ク%{t%} NounInfl-U ; !
ケマ:ケマ NounInfl-preU ; !
チェㇷ゚:チェㇷ゚ NounInfl-Nu ; !
ニシ:ニシ NounInfl-U ; !
ニマキ:ニマキ NounInfl-preU ; !
フラ:フラ NounInfl-Nu ; !
ペ:ペ%{pe%} NounInfl-Irreg ; !
ペㇳ:ペㇳ NounInfl-Irreg ; !
チコイキㇷ゚:チコイキㇷ゚ NounInfl-Common ; !
チセイ:チセイ NounInfl-Common ; !
シウェンテㇷ゚:シウェンテㇷ゚ NounInfl-Common ; !
クル:クル NounInfl-Common ; !
チェㇷ゚:チェㇷ゚ NounInfl-Nu ; !
クッペ:クッペ NounInfl-preU ; !
セタ:セタ NounInfl-Common ; !
アアㇰ:アアㇰ NounInfl-Common ; 
アトノ:アトノ NounInfl-Common ; 
アニㇱ:アニㇱ NounInfl-Common ; 
アポホ:アポホ NounInfl-Common ; 
アミチ:アミチ NounInfl-Common ; 
アユポ:アユポ NounInfl-Common ; 
アㇰ:アㇰ NounInfl-Common ; 
ト゚レㇱ:ト゚レㇱ NounInfl-Common ; 
シㇰ:シㇰ NounInfl-Common ;
カムィ:カムィ NounInfl-Common ;
サㇺ:サㇺ NounInfl-Relational ;



LEXICON AdjectiveRoot

モイレ:モイレ RegAdjectiveInfl ; ! slow
ラチ:ラチ RegAdjectiveInfl ; ! gentle
ソン:ソン IrregAdjectiveInfl ; ! true
ピリカ:ピリカ RegAdjectiveInfl ; 
ウェン:ウェン  RegAdjectiveInfl ; 
ポン:ポン  RegAdjectiveInfl ; 
ケラ:ケラ CompoundAdj ;
ハロ:ハロ CompoundAdj ;
ラム:ラム CompoundAdj ;
ヌチャㇰ:ヌチャㇰ CompoundAdj ;
アㇱカン:アㇱカン CompoundAdj ;
パロ:パロ CompoundAdj ;
ケㇺ:ケㇺ CompoundAdj ;
キ:キ CompoundAdj ;
ピリカ:ピリカ RegAdjectiveInfl ; 
ヌペキ:ヌペキ RegAdjectiveInfl ; ! bright
パセ:パセ RegAdjectiveInfl ; ! heavy
ポロ:ポロ RegAdjectiveInfl ; ! large





LEXICON VerbRoot

アマ:アマ RegVerbInfl ; !
クパ:クパ RegVerbInfl ; !
ペレパ:ペレパ RegVerbInfl ; !
テレケ:テレケ RegVerbInfl ; !
イェ:イェ RegVerbInfl ; !
イセ:イセ RegVerbInfl ; !
ミㇰ:ミㇰ RegVerbInfl ; ! run away
ミナ:ミナ RegVerbInfl ; ! bark
キラ:キラ RegVerbInfl ; ! laugh
イタㇰ:イタㇰ RegVerbInfl ; ! speak
エ:エ RegVerbInfl ; ! to be eaten
イェ:イェ RegVerbInfl ; ! to be spoken
ヌ:ヌ RegVerbInfl ; ! 
ヌイェ:ヌイェ RegVerbInfl ; ! 
ライケ:ライケ RegVerbInfl ; ! 
セ:セ RegVerbInfl ; !
クキㇰ:クキㇰ RegVerbInfl ; !
ルㇲカ:ルㇲカ RegVerbInfl ;
ネ:ネ Copula ; ! copula
ピㇼカ:ピㇼカ RegVerbInfl ; 
ウェン:ウェン  RegVerbInfl ; 
ハロコロ:ハロコロ RegVerbInfl ; 
ポン:ポン RegVerbInfl ;



LEXICON Conjunction

コ:コ ConjunctionInfl ;


LEXICON Determiner

ト゚ㇷ゚:ト゚ㇷ゚%<bn%>%<ndet%> # ;




! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst

